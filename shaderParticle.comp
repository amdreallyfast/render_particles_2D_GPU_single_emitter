#version 430 core

struct Particle
{
    vec2 _position;
    vec2 _velocity;
    int _isActive;
};

// work item indices for the particle array
// Note: This layout must be specified in the following style.  Replacing "local_size_x" with 
// "localSizeX" results in a compile error.  The GLSL compiler reduces everything to lower case,
// so "local_Size_X" is still fine.
// Also Note: The sizes here MUST (??you sure??) match the work group sizes specified when 
// calling glDispatchCompute(...).
layout (local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

layout (std430, binding = 0) buffer ParticleBuffer {
    Particle AllParticles[];
};

uniform float uDeltaTimeSec;     // self-explanatory
uniform float uRadiusSqr;
uniform vec2 uEmitterCenter;
uniform uint uMaxParticlesEmittedPerFrame;
uniform uint uMaxParticleCount;
//??layout atomic counter for particlesEmittedThisFrame? see https://www.opengl.org/wiki/Atomic_Counter ??

void UpdateParticle(uint bufferIndex)
{
    // as OpenGL 4.4, compute shaders don't have C's idea of pointers or C++'s idea of 
    // reference, so make a copy of the particle, work with it, and copy it back in
    Particle p = AllParticles[bufferIndex];

    // update position
    p._position += (p._velocity * uDeltaTimeSec);
    
    // if it went out of bounds, restart it
    vec2 distToCenter = p._position - uEmitterCenter;
    float distSqr = dot(distToCenter, distToCenter);
    if (distSqr > uRadiusSqr)
    {
        // just a simple reset for now
        p._position = uEmitterCenter;
    }

    // copy it back in
    AllParticles[bufferIndex] = p;
}

void main()
{
    // pluck out the index of the work item for this run of the shader
    // Note: I am dealing with a one dimensional array, and the only index variance was defined 
    // earlier to be in X, so pluck out the X.
    // Also Note: The number of dispatched work groups may result in an index that is beyond the 
    // maximum number of particles, so check the value against the max.
    uint index = gl_GlobalInvocationID.x;
    if (index >= uMaxParticleCount)
    {
        return;
    }

    UpdateParticle(index);
}

